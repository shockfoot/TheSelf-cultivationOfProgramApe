# 顶点/片元着色器的基本结构

基本的顶点/片元着色器结构如下：

``` ShaderLab
Shader "MyCustom/VertFragShader"
{
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            float4 vert(float4 v : POSITION) : SV_POSITION
            {
                return mul (UNITY_MATRIX_MVP, v);
            }

            fixed4 frag() : SV_Target
            {
                return fixed4(1.0, 0.0, 0.0, 1.0);
            }
            ENDCG
        }
    }
}
```

其中：

- `#pragma`两行编译指令指定顶点着色器和片元着色器，`vert`和`frag`是函数名。
- **`vert`函数逐顶点执行。** 本例中，`vert`函数输入`v`包含了顶点的位置，这是通过`POSITION`语义指定的。输出一个`float4`类型变量，是该顶点在裁剪空间中的位置，这是由`SV_POSITION`语义指定的。函数体中利用Unity内置的模型-观察-投影矩阵将顶点坐标从模型空间转换到裁剪空间中。
- **`frag`函数逐片元执行。片元着色器的输入实际上是把顶点着色器的输出进行插值后得到的结果。** 本例中，`frag`函数无输入。输出一个`float4`类型变量，`SV_Target`语义表明将输出颜色存储到一个渲染目标（Render Target）中，这里将输出到默认的帧缓存中。

在CG语法中，顶点着色器至少需要一个语义为`POSITION`的输入，至少输出一个语义为`SV_POSITION`的位置。片元着色器输出一个语义为`COLOR0`或`COLOR`的颜色，至少需要一个从顶点着色器输出的位置。**当顶点着色器只输出一个`SV_POSITION`时，该输出可以不需要在片元着色器中显示传入**，它会被图形硬件自行处理，片元着色器只需要关注颜色即可。

# 获取模型数据

如果想要获取更多的模型数据，如法线方向和纹理坐标（法线用于计算光照，纹理坐标用于访问纹理），需要为顶点着色器定义新的输入参数。

``` ShaderLab
Shader "MyCustom/VertFragShader"
{
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            // 使用结构体定义顶点着色器输入
            struct a2v // application to vertex shader
            {
                float4 vertex : POSITION; // 顶点坐标
                float3 normal : NORMAL; // 法线方向
                float4 texcoord : TEXCOORD0; // 第一套纹理坐标
            };

            float4 vert(a2v v) : SV_POSITION
            {
                return mul (UNITY_MATRIX_MVP, v.vertex);
            }

            fixed4 frag() : SV_Target
            {
                return fixed4(1.0, 0.0, 0.0, 1.0);
            }
            ENDCG
        }
    }
}
```

Unity会根据语义填充输入结构体，因此语义是不可省略的。对于顶点着色器的输入，Unity支持的语义有：顶点位置`POSITION`、切线`TANGENT`、法线`NORMAL`、第一套纹理坐标`TEXCOORDO`、第二套纹理坐标`TEXCOORD1`、第三套纹理坐标`TEXCOORD2`、第四套纹理坐标`TEXCOORD3`、顶点颜色`COLOR`等。

填充到`POSJTION`、`NORMAL`这些语义中的数据是由Unity中使用该材质的Mesh Render组件提供的。在每帧调用Draw Call的时候Mesh Render组件会把它负责渲染的模型数据发送给Unity Shader。一个模型通常包含了一组三角面片，每个三角面片由3个顶点构成，而每个顶点又包含了一些数据，如顶点位置、法线、切线、纹理坐标、顶点颜色等。通过上面的方法，可以在顶点着色器中访问顶点的这些模型数据。

# 顶点着色器与片元着色器通信

上述顶点着色器都输出裁剪空间中的顶点坐标，片元着色器会自动获取顶点着色器的输出。如果片元着色器需要获取更多的信息，如法线、纹理坐标等，需要为顶点着色器定义新的输出参数，并传入片元着色器。

``` ShaderLab
Shader "MyCustom/VertFragShader"
{
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            struct a2v
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            // 使用结构体定义顶点着色器输出以传给片元着色器
            struct v2f
            {
                float4 pos : SV_POSITION; // 顶点坐标
                fixed3 color : COLOR0; // 颜色
            }

            v2f vert (a2v v)
            {
                // 声明输出结构
                v2f rel;
                rel.pos = UnityObjectToClipPos (v.vertex);
                // v.normal法线方向范围在[-1,1]，将分量范围映射到[0,1]之间并存储到rel.color中传给片元着色器
                rel.color = v.normal * 0.5 + fixed3(0.5, 0.5, 0.5);
                return rel;
            }

            fixed4 frag (v2f f) : SV_Target
            {
                // 将插值后的颜色显示在屏幕上
                return fixed4(f.color, 1.0);
            }
            ENDCG
        }
    }
}
```

结构体`v2f`用于保存顶点着色器的输出，并将输出传递给片元着色器。同样地，`v2f`中需要指定每个变量的语义。**顶点着色器的输出中，必须包含一个语义为`SV_POSITION`的`float4`变量。** 否则片元着色器无法得到裁剪空间中的顶点坐标，也就无法将其渲染到屏幕上。

# 使用属性

属性可以方便调控渲染效果。

``` ShaderLab
Shader "MyCustom/VertFragShader"
{
    Properties
    {
        // 声明一个Color类型的属性
        _Color ("Color", Color) = (1.0, 1.0, 1.0, 1.0)
    }

    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            // 需要定义一个与属性名称和类型都相匹配的变量
            fixed4 _Color;
            
            struct a2v
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            struct v2f
            {
                float4 pos : SV_POSITION;
                fixed3 color : COLOR0;
            }

            v2f vert (a2v v)
            {
                v2f rel;
                rel.pos = UnityObjectToClipPos (v.vertex);
                rel.color = v.normal * 0.5 + fixed3(0.5, 0.5, 0.5);
                return rel;
            }

            fixed4 frag (v2f f) : SV_Target
            {
                fixed3 c = f.color;
                // 使用_Color属性控制颜色
                c *= _Color.rgb;
                return fixed4(c, 1.0);
            }
            ENDCG
        }
    }
}
```

可以在`Properties`块中声明属性。为了在CG代码中访问之，必须在CG代码中提前定义一个与属性名称和类型相匹配的变量。

| ShaderLab属性类型 | CG变量类型 |
| --- | --- |
| `Color`，`Vector` | `float4`，`half4`，`fixed4` |
| `Range`，`Float` | `float`，`half`，`fixed` |
| `2D` | `simple2D` |
| `3D` | `simple3D` |
| `Cube` | `simpleCube` |

CG变和参数量可以使用`uniform`关键字修饰，仅仅用于提供一些关于该变量的初始值是如何指定和存储的相关信息。在Unity Shader中，`uniform`是可以省略的。